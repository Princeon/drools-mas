/*
 * Copyright 2011 JBoss Inc
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *       http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

/**
  Content Based routing general rules
  When a message arrives, CBR rules determine its destination (the session(s) where it needs to be processed),
  while processing rules determine the commands to be executed to deliver the contents to that session.
  When the two steps have been completed, the commands are delivered.
*/

package org.drools.mas.core;

import org.drools.mas.core.SessionManager;
import java.util.Map;
import org.drools.mas.util.LoggerHelper;
import org.drools.mas.core.DroolsAgentConfiguration;
import org.drools.mas.core.SessionManagerFactory;
import org.drools.mas.util.helper.SessionLocator;
import org.drools.mas.util.helper.NoHandlerFault
import org.kie.api.runtime.KieSession
import org.drools.mas.core.helpers.ResponseContent
import org.drools.mas.core.helpers.SessionHelper;

declare SessionGoal
    @Abducible
    key : String
end

query ensureSession( String key )
    @Abductive( target=SessionGoal.class )
end

query mapSession( String $key, String $sid )
    ?ensureSession( $key ; )
    SessionMapping( $key, $sid; )
end

rule "log"
when
    $s: SessionMapping()
then
    LoggerHelper.debug( "  !#!@#!@#!@#!@####  Session Mapping: " + $s );
end


rule "Trigger Mapping"
when
    $ml : MappingLookup( $mid, $key; )
    ?mapSession( $key, $target; )
then
    LoggerHelper.debug(" ### Agent CBR: Triggered routing destination for message " + $mid + " to session " + $target );
end

/**
Is this rule being used? Nobody is never inserting a MappingLookup.
CBR rules usually insert Destination directly.
**/
rule "Resolve Mapping"
when
    $ml : MappingLookup( $mid, $key; )
    SessionMapping( $key; $target : sessionId != null )
then
    LoggerHelper.debug(" ### Agent CBR: Resolved routing destination for message " + $mid + " to session <" + $target + ">");

    retract( $ml );
    insert( new Destination( $mid, $target ) );
end


rule "Generate Session"
salience 9999
when
    SessionGoal( $key : key != null && != "" )
    not SessionLocator( $key ;  )
    $conf: DroolsAgentConfiguration( $mutable : defaultSubsessionDescriptor.mutable )
    $sessionLocator: SessionLocator( mind == true )
then
    KieSession kSession = SessionHelper.createSessionOnTheFly( $key, $conf );
    kSession.insert( new SessionLocator( $key , false, true, $mutable ) );
    kSession.insert( $sessionLocator );
    kSession.fireAllRules();

    SessionMapping sm = new SessionMapping( $key, $key ) ;
    insert( sm );

    insert( new SessionLocator( $key , false, true, $mutable ) );
end


rule "No session of this kind of message"
    salience -10000
    when
        $s: SessionLocator( $sessionId: sessionId, mind == true )
        ACLMessage( $msgId : id  )
        not Destination( $msgId; )
    then
        ResponseContent response = new ResponseContent( $sessionId, $msgId, null );
            response.setFault( new NoHandlerFault( $msgId, "CRITICAL : No Internal Routing Rule for this Message! " ) );
        insert( response );
end






